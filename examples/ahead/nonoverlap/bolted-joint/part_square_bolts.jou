# create plate and attachment
#{offset = 0.005}

undo off
brick x 5 y 0.5 z 5
move volume 1 y 0.25 include_merged
brick x 3.5 y 0.25 z 3.5
move volume 2 y {0.125 + 0.5 + offset} include_merged

brick x 2 y 2.25 z 2
brick x 1.5 y 2.0 z 1.5
move volume 4 y -0.125
subtract volume 4 from volume 3

tweak curve 49 53 55 51 25 29 31 27 fillet radius 0.5
move volume 3 y {1.125 + 0.5 + offset} include_merged

subtract volume 3 from volume 2 keep
split body 5
delete volume 2
delete volume 6

tweak curve 119 117 121 123 fillet radius 0.5
tweak curve 1 3 5 7 fillet radius 0.5

#{cut_cube_side = 0.6}

brick x {cut_cube_side} y {3} z {cut_cube_side}
move volume 7 location 1.25 {(0.75 + offset)/2.0} 1.25 include_merged
volume 7 copy move z -2.5
volume 7 copy move x -2.5
volume 7 copy move x -2.5 z -2.5
subtract volume 7 8 9 10 from volume 1 5

imprint volume 5 3 
merge volume 5 3

#{coarse_mesh = 0.125}
#{cm_mesh = (coarse_mesh+medium_mesh)/2.0}
#{medium_mesh = 0.0625}

# mesh main housing with hexes
volume 3 size {cm_mesh}
mesh volume 3


# top surface of component plate
surface 129 size {cm_mesh}
# curves around holes on top surface
curve 288 291 281 285 312 315 305 309 300 303 293 297 324 327 317 321 size {medium_mesh}
curve 288 291 281 285 312 315 305 309 300 303 293 297 324 327 317 321  scheme equal
surface 129 scheme pave
# mesh top surface of component plate
mesh surface 129

# sweep top surface to bottom surface
volume 5 autosmooth target off
volume 5 redistribute nodes off
volume 5 scheme sweep source surface 129 target surface 130 sweep transform translate propagate bias
mesh volume 5



# bottom surface of plate
surface 111 size {coarse_mesh}
# curves on bottom surface
curve 245 255 249 252 269 273 276 279 233 243 240 237 257 267 261 264 size {medium_mesh}
curve 245 255 249 252 269 273 276 279 233 243 240 237 257 267 261 264 scheme equal
surface 111 scheme pave
# mesh bottom surface
mesh surface 111

# mesh inside of holes of bottom plate
surface 102 99 100 101 110 107 108 109 98 95 96 97 106 103 104 105 size {coarse_mesh}
mesh surface 102 99 100 101 110 107 108 109 98 95 96 97 106 103 104 105

# mesh plate
volume 1 autosmooth target off
volume 1 redistribute nodes off
volume 1 scheme sweep source surface 111 target surface 112 sweep transform translate propagate bias
mesh volume 1

# assign blocks
block 1 volume 1 3 5 
block 1 name "component"

# assign nodesets

# boundary of base plate
nodeset 1 surface 4 69 1 70 6 68 2 67 
nodeset 1 name "base_plate"
# boundary for loading
nodeset 2 surface 17
nodeset 2 name "loading"
# boundaries for schwarz
sideset 1 surface 128 125 126 127 120 117 118 119 116 113 114 115 124 121 122 123 99 102 101 100 95 98 97 96 103 106 105 104 107 110 109 108
sideset 1 name "schwarz_ss_parts"

body all scale 0.0254 0.0254 0.0254

#export to genesis
export mesh "part_square_bolts.g"  overwrite



